apply from: 'gradle/scripts/helpers.gradle'

def getVersionFromFile(String fileName) {
    def versionFile = file(fileName)
    if (versionFile.exists()) {
        return versionFile.text.trim()
    } else {
        throw new Exception("File not found: ${versionFile.absolutePath}") as Throwable
    }
}
def tool_version = getVersionFromFile('./tool_version.txt')
def mod_version = getVersionFromFile('./version.txt')

repositories {
    // Other repositories described by default:
    // CleanroomMC: https://maven.cleanroommc.com
    exclusiveContent {
        forRepository {
            maven {
                name 'CurseMaven'
                url 'https://cursemaven.com'
            }
        }
        filter {
            includeGroup 'curse.maven'
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name 'Modrinth'
                url 'https://api.modrinth.com/maven'
            }
        }
        filter {
            includeGroup 'maven.modrinth'
        }
    }
    maven {
        name 'GitHub Packages'
        url 'https://maven.pkg.github.com/17TheWord/QueQiaoTool'
        credentials {
            username = System.getenv("USERNAME")
            password = System.getenv("PACKAGE_READ_ONLY_TOKEN")
        }
    }
    flatDir {
        dirs 'libs'
    }
    mavenLocal() // Must be last for caching to work
}

dependencies {
    // Include StripLatestForgeRequirements by default for the dev env, saves everyone a hassle
    runtimeOnly 'com.cleanroommc:strip-latest-forge-requirements:1.0'

    // For easier testing
    runtimeOnly rfg.deobf('curse.maven:had-enough-items-557549:4543375')
    runtimeOnly rfg.deobf('curse.maven:ftb-library-legacy-forge-237167:2985811')
    runtimeOnly rfg.deobf('curse.maven:ftb-utilities-forge-237102:3157548')

    implementation("com.github.theword.queqiao:queqiao-tool:${tool_version}") {
        // 排除 slf4j
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.yaml', module: 'snakeyaml'
    }
    implementation "org.java-websocket:Java-WebSocket:${java_websocket_version}"
    implementation "org.slf4j:slf4j-simple:${slf4j_version}"
    implementation 'org.yaml:snakeyaml:1.33'
    compileOnly "org.projectlombok:lombok:${lombok_version}"
    annotationProcessor "org.projectlombok:lombok:${lombok_version}"
}

shadowJar {
    archiveFileName = "QueQiao-forge+${minecraft_version}-${mod_version}.jar"
    dependencies {
        include(dependency("com.github.theword.queqiao:queqiao-tool:${tool_version}"))
        include(dependency("org.java-websocket:Java-WebSocket:${java_websocket_version}"))
        include(dependency('org.slf4j:slf4j-api:.*'))
        include(dependency('org.slf4j:slf4j-simple:.*'))
        include(dependency('org.yaml:snakeyaml:1.33'))
    }
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': 'com.github.theword.queqiao.QueQiao'
    }
    relocate 'org.slf4j', 'shadow.org.slf4j'
    relocate 'org.slf4j.impl', 'shadow.org.slf4j.impl'
}
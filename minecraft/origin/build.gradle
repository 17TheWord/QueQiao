plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

def getVersionFromFile() {
    def versionFile = file('../../version.txt')
    if (versionFile.exists()) {
        return versionFile.text.trim()
    } else {
        throw new Exception("Version file not found: ${versionFile.absolutePath}") as Throwable
    }
}

def mod_version = getVersionFromFile()

def getToolVersionFromFile() {
    def versionFile = file('../../tool_version.txt')
    if (versionFile.exists()) {
        return versionFile.text.trim()
    } else {
        throw new Exception("Version file not found: ${versionFile.absolutePath}") as Throwable
    }
}

def tool_version = getToolVersionFromFile()

group = mod_group_id
version = mod_version

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = uri(maven_repository_url)
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("PACKAGE_READ_ONLY_TOKEN")
        }
    }
}
dependencies {
    annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
    compileOnly "org.projectlombok:lombok:${project.lombok_version}"

    implementation "com.github.theword.queqiao:queqiao-tool:${tool_version}"
    implementation "commons-io:commons-io:${project.commons_io_version}"
    implementation "org.slf4j:slf4j-simple:${slf4j_version}"
    implementation "com.google.code.gson:gson:${gson_version}"

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

shadowJar {
    archiveBaseName.set("${mod_name}-minecraft+origin")
    archiveClassifier.set("")
    destinationDirectory.set(file("../../QueQiao-jar/${version}"))
    manifest {
        attributes(
                'Main-Class': 'com.github.theword.queqiao.QueQiao' // 替换为你的主类路径
        )
    }
    dependencies {
        include(dependency("com.github.theword.queqiao:queqiao-tool:${tool_version}"))
        include(dependency("org.java-websocket:Java-WebSocket:${java_websocket_version}"))
        include(dependency("org.yaml:snakeyaml:${snakeyaml_version}"))
        include(dependency("org.slf4j:slf4j-api:${slf4j_version}"))
        include(dependency("org.slf4j:slf4j-simple:${slf4j_version}"))
        include(dependency("com.google.code.gson:gson:${gson_version}"))
        include(dependency("commons-io:commons-io:${commons_io_version}"))
    }
    mergeServiceFiles()
}

tasks.build {
    dependsOn shadowJar
}